#! /bin/csh -f

# set up $JSOC_MACHINE
set JSOC_MACHINE = `build/jsoc_machine.csh`
echo "Machine type is $JSOC_MACHINE"
echo ""

set CLEAN = "no"
set CHKLIBS = "no"
set CCMD = "c"
set LCMD = "l"

foreach ARG ($argv)
    set FLAG = `echo $ARG | awk '{print substr($0, 2)}'`
    if ($FLAG == $CCMD) then
      set CLEAN = "yes"
    else if ($FLAG == $LCMD) then
      set CHKLIBS = "yes"
    endif
end

if ($CHKLIBS == "no") then
    echo -n "Remove links for local project configuration..."
    if (-d proj) then
	cd proj
	if (-e configure) then
	    rm configure
	endif
	if (-e make_basic.mk) then
	    rm make_basic.mk
	endif
	if (-e Rules.mk) then
	    rm Rules.mk
	endif
	if (-e target.mk) then
	    rm target.mk
	endif
	cd ..
    endif
    echo "done"

    echo -n "Remove links to man pages..."
    if (-e man) then
	rm -rf man
    endif
    echo "done"

    echo -n "Removing links to headers..."
    if (-d base/include) then
	cd base/include
	find . -name "*.h" -exec rm {} \;
	cd ../..
    endif

    if (-d include) then
	cd include
	find . -name "*.h" -exec rm {} \;
	cd ..
    endif
    echo "done"

    echo -n "Removing links to scripts..."
    if (-d scripts) then
	cd scripts
	find . -name "*" -exec rm {} \;
	cd ..
    endif
    echo "done"

    echo -n "Removing links to jsds..."
    if (-d jsds) then
	cd jsds
	find . -name "*" -exec rm {} \;
	cd ..
    endif
    echo "done"

    echo -n "Removing links to third-party libraries..."
    if (-d lib_third_party) then
	rm -rf lib_third_party
    endif
    echo "done"

    echo

    if ($CLEAN == "yes") then
	if (-x base/local/libs/dsds/scripts/rmlinks.csh) then
	    base/local/libs/dsds/scripts/rmlinks.csh
	endif

	if (-x base/local/libs/soi/scripts/rmlinks.csh) then
	    base/local/libs/soi/scripts/rmlinks.csh
	endif
    else
	echo -n "Setting links for local project configuration..."
	if (-e suflag.txt) then
	    cd proj
	    ln -sf ../projconf/su/configure configure
	    ln -sf ../projconf/su/make_basic.mk make_basic.mk
	    ln -sf ../projconf/su/Rules.mk Rules.mk
	    ln -sf ../projconf/su/target.mk target.mk
	    cd ..
	else
	    cd proj
	    ln -sf ../projconf/ex/configure configure
	    ln -sf ../projconf/ex/make_basic.mk make_basic.mk
	    ln -sf ../projconf/ex/Rules.mk Rules.mk
	    ln -sf ../projconf/ex/target.mk target.mk
	    cd ..
	endif
	echo "done"

	echo "Setting links to man pages..."
	if (-d /home/jsoc/man) then
	    if (!(-e man)) then
		ln -s /home/jsoc/man man
	    endif
	endif
	echo "done"

	echo "Setting links to headers..."
	if (!(-d base/include)) then
	    mkdir -p base/include
	endif
	cd base/include
	find .. -path '../include' -prune -o -name \*.h  \( \( -exec ln -s {} . \; -a -exec echo "  " {} " OK" \; \) -o -exec echo "  " {} " ERROR - couldn't create link" \; \)
	cd ../..

	if (!(-d include)) then
	    mkdir -p include
	endif
	cd include
	if (-d ../base/include) then
	    find ../base/include -name \*.h -exec ln -s {} . \;
	endif
	cd ..
	echo "done"
	echo
	
	echo "Setting links to scripts..."
        if (!(-d scripts)) then
	    mkdir scripts
	endif
	cd scripts

	# Sums scripts
	find ../base/sums/scripts -mindepth 1 \( -path \*CVS -prune -o -exec ln -s {} . \; -a -exec echo "  " {} " OK" \; \) -o -exec echo "  " {} " ERROR - couldn't create link" \;

	# Util scripts
	find ../base/util/scripts -mindepth 1 \( -path \*CVS -prune -o -exec ln -s {} . \; -a -exec echo "  " {} " OK" \; \) -o -exec echo "  " {} " ERROR - couldn't create link" \;

	# DRMS / IDL-interface scripts
	find ../base/drms/scripts -mindepth 1 \( -path \*CVS -prune -o -exec ln -s {} . \; -a -exec echo "  " {} " OK" \; \) -o -exec echo "  " {} " ERROR - couldn't create link" \;

	# Project-specific scripts - these won't show up if you don't have the project source.
	if (-x ../proj/configure) then
	    ../proj/configure
	endif

	cd ..
	echo "done"
	echo

	echo "Setting links to jsds..."
	if (!(-d jsds)) then
	    mkdir jsds
	endif
	cd jsds
	find ../base \( -name \*.jsd -exec ln -s {} . \; -a -exec echo "  " {} " OK" \; \) -o \( -name \*.jsd -exec echo "  " {} " ERROR - couldn't create link" \; \)
	cd ..
	echo "done"
	echo
	# Set links to third-party libraries
	echo "Setting links to third-party libraries"
	if (!(-d lib_third_party)) then
	    mkdir lib_third_party
	endif
	cd lib_third_party

	if (-e ../suflag.txt) then
            if (!(-d include)) then
                mkdir include
            endif

            if (!(-d lib)) then
                mkdir lib
            endif

            # Don't make a link directly from include/lib to a directory, else
            # all includes files/libs must reside in one directory.

            cd include
            # Standard location for all of JSOC's includes
            find /home/jsoc/include -maxdepth 1 -name "*.h" -exec ln -sfv {} \;
            find /home/jsoc/include -mindepth 1 -type d -exec ln -sfv {} \;
            cd ..

            cd lib
            # Standard locations for all of JSOC's libraries - x86_64
            if (!(-d linux_x86_64)) then
                mkdir linux_x86_64
            endif

            cd linux_x86_64
                find /home/jsoc/lib/linux_x86_64/ -maxdepth 1 -name "*.a" -exec ln -sfv {} \;
            cd ..

            # Standard locations for all of JSOC's libraries - ia32
            if (!(-d linux_ia32)) then
                mkdir linux_ia32
            endif

            cd linux_ia32
                find /home/jsoc/lib/linux_ia32/ -maxdepth 1 -name "*.a" -exec ln -sfv {} \;
            cd ..

            cd ..
	else
# Non-Stanford users: edit these lines to reflect the location of required
#  3rd party libs: cfitsio.a
	    ln -sfv /usr/local/include include
            if (!(-d lib)) mkdir lib
	    ln -sfv /usr/local/lib lib/$JSOC_MACHINE
	endif

	set TPWARN = 0

	# Libraries required by all
	if ($JSOC_MACHINE == "linux_x86_64") then
	    if (!(-e lib/linux_x86_64/libcfitsio.a)) then
		echo "Can't find required library: lib/linux_x86_64/libcfitsio.a"
		set TPWARN = 1
	    endif
	endif

	if ($JSOC_MACHINE == "linux_ia32") then
	    if (!(-e lib/linux_ia32/libcfitsio.a)) then
		echo "Can't find required library: lib/linux_ia32/libcfitsio.a"
		set TPWARN = 1
	    endif
	endif

	if ($JSOC_MACHINE == "mac_osx") then
	    if (!(-e lib/mac_osx/libcfitsio.a)) then
		echo "Can't find required library: lib/mac_osx/libcfitsio.a"
		set TPWARN = 1
	    endif
	endif

	if (-e ../suflag.txt) then
	    # Stanford users' required libs.
	    if ($JSOC_MACHINE == "linux_x86_64") then
		if (!(-e lib/linux_x86_64/libfftw3f.a)) then
		    echo "Can't find required library: lib/linux_x86_64/libfftw3f.a"
		    set TPWARN = 1
		endif
		if (!(-e lib/linux_x86_64/libgsl.a)) then
		    echo "Can't find required library: lib/linux_x86_64/libgsl.a"
		    set TPWARN = 1
		endif
		if (!(-e lib/linux_x86_64/libgslcblas.a)) then
		    echo "Can't find required library: lib/linux_x86_64/libgslcblas.a"
		    set TPWARN = 1
		endif
	    endif

	    if ($JSOC_MACHINE == "linux_ia32") then
		if (!(-e lib/linux_ia32/libfftw3f.a)) then
		    echo "Can't find required library: lib/linux_ia32/libfftw3f.a"
		    set TPWARN = 1
		endif
		if (!(-e lib/linux_ia32/libgsl.a)) then
		    echo "Can't find required library: lib/linux_ia32/libgsl.a"
		    set TPWARN = 1
		endif
		if (!(-e lib/linux_ia32/libgslcblas.a)) then
		    echo "Can't find required library: lib/linux_ia32/libgslcblas.a"
		    set TPWARN = 1
		endif
	    endif

	    if ($JSOC_MACHINE == "mac_osx") then
		if (!(-e lib/mac_osx/libfftw3f.a)) then
		    echo "Can't find required library: lib/mac_osx/libfftw3f.a"
		    set TPWARN = 1
		endif
	    endif
	else
	    # Non-Stanford users' required libs.
	endif

	if ($TPWARN) then
	    echo "Did you update configure to create links to installed third-party libraries?"
	endif

	cd ..
	echo "done"
	echo

	# generate links for DSDS/SOI dynamic libraries - only do this if 
	# user's environment has access to /home/soi/CM
	if (-d /home/soi/CM/include && -d /home/soi/CM/src) then
	    if (-x base/local/libs/dsds/scripts/genlinks.csh) then
		base/local/libs/dsds/scripts/genlinks.csh
	    endif

	    if (-x base/local/libs/soi/scripts/genlinks.csh) then
		base/local/libs/soi/scripts/genlinks.csh
	    endif
	endif
    endif
endif

# Enter required library names and versions here.
if ($CHKLIBS == "yes") then

    echo
    echo "JSOC expects all third-party library header files to be located at $JSOCROOT/lib_third_party/include/ and all third-party libraries to be located at $JSOCROOT/lib_third_party/$JSOC_MACHINE/.  If you wish to build a target that uses one of the following third-paty libraries, please install the required library version and create a link from from $JSOCROOT/lib_third_party/include/ and $JSOCROOT/lib_third_party/$JSOC_MACHINE/ to the installed library headers and binary files, respectively."
    echo
    echo "Example:"
    echo "  cd $JSOCROOT/lib_third_party/"
    echo "  ln -s /usr/local/include include"
    echo "  cd $JSOCROOT/lib_third_party/$JSOC_MACHINE/"
    echo "  ln -s /usr/local/lib/libfftw3f.a libfftw3f.a "
    echo
    echo "Library libfftw3f.a (v. 3.1.2) required for targets: universe, examples, helloworld, xinterp, demo_td08062007"
    echo "Library libcfitsio.a (v. 3.03) required for targets: universe, examples, f_ingest_gong_mrv, f_dup_gong_mrv"

#    echo "Library libcfitsio.a (v. 3.03) required for targets: examples, helloworld"
#    echo "Library libmkl.so (v. 9.0) required for targets: examples, helloworld"
#    if ($JSOC_MACHINE == "linux_x86_64") then 
#	echo "Library libmkl_lapack64.so (v. 9.0) required for targets: examples, helloworld"
#    else echo "Library libmkl_lapack32.so (v. 9.0) required for targets: examples, helloworld"
#    endif


endif 

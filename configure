#! /bin/csh -f

# system configuration for configure script
set PERLBIN = ""

if ($PERLBIN == "") then
    if (-e configsdp.txt) then
        # at stanford, use 
        set PERLBIN = "/home/jsoc/bin/linux_x86_64/perl5.12.2"
    else
        set PERLBIN = "perl"
    endif
endif

# set up $JSOC_MACHINE
set JSOC_MACHINE = `build/jsoc_machine.csh`
echo "Machine type is $JSOC_MACHINE"
echo ""

set CLEAN = "no"
set PROJCONFIG = ""
set CLEANCMD = "d"

set CUSTOMDEFSFILE = ""

foreach THEARG ($argv)
    set LITERALSTR = '{ my($argin) = "'${THEARG}'"; my($flagout); $flagout = ($argin =~ /^-(.+)/)[0]; print $flagout; }'
    set FLAG = `$PERLBIN -e "$LITERALSTR"`
    
    if ($FLAG == $CLEANCMD) then
        set CLEAN = "yes"
    else if (-e $THEARG) then
        set PROJCONFIG = $THEARG
    endif
end

# Usage:
#   configure [ -c ] [ <project config file> ]

# Backward compatibility - remove the links to the old make files in projconf
if (-d proj) then	 
    cd proj	 
    if (-e configure) then	 
        rm configure	 
    endif	 
    if (-e make_basic.mk) then	 
        rm make_basic.mk	 
    endif	 
    if (-e Rules.mk) then	 
        rm Rules.mk	 
    endif	 
    if (-e target.mk) then	 
        rm target.mk	 
    endif	 
    cd ..	 
endif

    echo -n "Remove links to man pages..."
    if (-e man) then
	rm -rf man
    endif
    echo "done"

    echo -n "Removing links to headers..."
    if (-d base/include) then
	cd base/include
	find . -name "*.h" -exec rm {} \;
	cd ../..
    endif

    if (-d include) then
	cd include
	find . -name "*.h" -exec rm {} \;
	cd ..
    endif
    echo "done"

    echo -n "Removing links to scripts..."
    if (-d scripts) then
	cd scripts
	find . -name "*" -exec rm {} \;
	cd ..
    endif
    echo "done"

    echo -n "Removing links to jsds..."
    if (-d jsds) then
	cd jsds
	find . -name "*" -exec rm {} \;
	cd ..
    endif
    echo "done"

    echo

    if ($CLEAN == "yes") then
	if (-x base/local/libs/dsds/scripts/rmlinks.csh) then
	    base/local/libs/dsds/scripts/rmlinks.csh
	endif

	if (-x base/local/libs/soi/scripts/rmlinks.csh) then
	    base/local/libs/soi/scripts/rmlinks.csh
	endif
    else
        # Make include directories here, since they are used by multiple script blocks below
        if (!(-d base/include)) then
            mkdir -p base/include
        endif

        if (!(-d include)) then
            mkdir -p include
        endif

	echo "Setting links to man pages..."
	if (-d /home/jsoc/man) then
	    if (!(-e man)) then
		ln -s /home/jsoc/man man
	    endif
	endif
	echo "done"
        echo
	
	echo "Setting links to jsds..."
	if (!(-d jsds)) then
	    mkdir jsds
	endif
	cd jsds
	find ../base \( -name \*.jsd -exec ln -s {} . \; -a -exec echo "  " {} " OK" \; \) -o \( -name \*.jsd -exec echo "  " {} " ERROR - couldn't create link" \; \)
	cd ..
	echo "done"
	echo

	# generate links for DSDS/SOI dynamic libraries - only do this if 
	# user's environment has access to /home/soi/CM
	if (-d /home/soi/CM/include && -d /home/soi/CM/src) then
	    if (-x base/local/libs/dsds/scripts/genlinks.csh) then
		base/local/libs/dsds/scripts/genlinks.csh
	    endif

	    if (-x base/local/libs/soi/scripts/genlinks.csh) then
		base/local/libs/soi/scripts/genlinks.csh
	    endif
	endif

	echo "done"
	echo

        # Site Localization

        # Path to the configuration file - at some point, make this an argument to the configure script
        set LOCALIZATIONDIR = ""
        set conflocal = "config.local"

        if (-e $conflocal) then
            set LOCALIZATIONDIR = `perl -n -e 'if ($_ =~ /^LOCALIZATIONDIR\s+(.+)/) { print $1; }' $conflocal`
        endif

        if ($LOCALIZATIONDIR == "") then
            set LOCALIZATIONDIR = ${PWD}/localization
        endif

        # Eventually, dump all these generated files into a user-specified location.
        # This script will need to read the config.local file to get the 
        # localization directory. Otherwise, just use JSOC/localization as the default.
        # We should put custom.mk in the localization directory too.
        if (!(-d ${LOCALIZATIONDIR})) then
            mkdir ${LOCALIZATIONDIR}
            if ($? != 0) then
                echo "Error creating localization directory."
            endif
        endif
        
        if (-e ${LOCALIZATIONDIR}/custom.mk) then
            rm ${LOCALIZATIONDIR}/custom.mk
        endif

        # Must create the scripts subdirectory before the call to gen_init.csh
        if (!(-d scripts)) then
	    mkdir scripts
	endif

        if (!(-e configsdp.txt)) then
            # This is NetDRMS.
            if (!(-e config.local)) then
                echo "Error: config.local not found. If you have a saved version of this"
                echo "       file from a previous installation, you should copy that into"
                echo "       this directory and rerun configure. Otherwise, copy the file"
                echo "       config.local.template to config.local and edit the config.local"
                echo "       file to contain site-appropriate values."
                echo 
                echo "*** IMPORTANT *** The edited config.local will contain site-specific"
                echo "    values that should not be modified during subsequent updates to"
                echo "    NetDRMS. Keep a safe copy of it somewhere outside of the DRMS"
                echo "    directory tree, for example in /usr/local. After each NetDRMS"
                echo "    update, ensure this file is in place."
                exit 1;
            else
                echo "CUSTOM_DEFINES = __LOCALIZED_DEFS__" >> ${LOCALIZATIONDIR}/custom.mk

                set CUSTOMDEFSFILE = "base/include/localization.h"
                echo "*** generating $CUSTOMDEFSFILE ***"
                cat /dev/null > $CUSTOMDEFSFILE
                echo '#ifndef __LOCALIZATION_H' >> $CUSTOMDEFSFILE
                echo '#define __LOCALIZATION_H' >> $CUSTOMDEFSFILE
                ./gen_init.csh

                # Non-customizable parameters. There are certain features that are not
                # present in NetDRMS, but that are present in the SDP JSOC version of DRMS.
                # Set defines needed to accomplish this here.
                # NetDRMS sites do not have DSDS.
                echo '#define DSDS_SUPPORT (0)' >> $CUSTOMDEFSFILE
                echo '#endif' >> $CUSTOMDEFSFILE

                # run moreconfigure.pl - this script is written in perl because it is easier to do the compiler detection 
                # in perl. It also reads configdsp.txt (if it exists) to set the default Stanford values for certain make variables
                $PERLBIN ./moreconfigure.pl -d${LOCALIZATIONDIR} -fcustom.mk -c${CUSTOMDEFSFILE}

                # must follow moreconfigure.pl, because moreconfigure.pl will possibly set the compiler make variables,
                # which are then referenced by lines in custom.mk generated by customizemake.pl.
                $PERLBIN ./customizemake.pl ./config.local ${LOCALIZATIONDIR}
            endif
        else if (-e config.local) then
            # This is Stanford.

            # config.local is optional at Stanford, but it won't necessarily contain the same information that 
            # a NetDRMS config.local contains. The first section contains items that will override the
            # the items in serverdefs.h. The second section contains items used to populate custom.mk, 
            # which causes make systems variables to be defined that override the default make behavior
            # (such as overriding the name of the directory to which binary data are written - linux_x86_64_n02
            # instead of linux_x86_64, for example).

            # Override the defaults defined in serverdefs.h (don't use gen_init.csh as it does several things, 
            # most of which are relevant only to non-SU NetDRMS sites). This script causes all 
            # items included in config.local to override those defined in serverdefs.h, but not all
            # items need be specified in config.local.
            echo "CUSTOM_DEFINES = __CUSTOMIZED_DEFS__" >> ${LOCALIZATIONDIR}/custom.mk
            $PERLBIN ./customizedefs.pl

            set CUSTOMDEFSFILE = "base/include/customizeddefs.h"

            # run moreconfigure.pl - this script is written in perl because it is easier to do the compiler detection 
            # in perl. It also reads configsdp.txt (if it exists) to set the default Stanford values for certain make variables
            $PERLBIN ./moreconfigure.pl -d${LOCALIZATIONDIR} -fcustom.mk -c${CUSTOMDEFSFILE}

            # update custom.mk with data from config.local
            # must follow moreconfigure.pl, because moreconfigure.pl will possibly set the compiler make variables,
            # which are then referenced by lines in custom.mk generated by customizemake.pl.
            $PERLBIN ./customizemake.pl ./config.local ${LOCALIZATIONDIR}
        else
            # run moreconfigure.pl - this script is written in perl because it is easier to do the compiler detection 
            # in perl. It also reads configsdp.txt (if it exists) to set the default Stanford values for certain make variables
            $PERLBIN ./moreconfigure.pl -d${LOCALIZATIONDIR} -fcustom.mk 
        endif

        # Figure out if this is a custom checkout
        set CUSTCO = "no"
        set LITERALSTR = '{ open(CFG, "<'${LOCALIZATIONDIR}'/dlset.txt") || die "BAD"; my $line = <CFG>; chomp($line); print $line; }'
        set CUSTCO = `$PERLBIN -e "$LITERALSTR"`

        if (${CUSTCO} == "BAD") then
            echo "Unable to open checkout state file ${LOCALIZATIONDIR}/dlset.txt"
            exit 1
        else if (${CUSTCO} == "custom") then
            set CUSTCO = "yes"
        endif

        if (${PROJCONFIG} == "") then
            if (${CUSTCO} == "yes") then
                echo "Must provide configuration file as an argument to this configure script"
                exit 1;
            endif
            if (-e configsdp.txt) then
                set PROJCONFIG = configsdp.txt
            endif
        endif

        if (${PROJCONFIG} != "") then
            echo "Running project configuration [${PERLBIN} configproj.pl -c${PROJCONFIG} -d${LOCALIZATIONDIR}]."
            ${PERLBIN} configproj.pl -c${PROJCONFIG} -d${LOCALIZATIONDIR}
        endif

	echo "Setting links to scripts..."
	cd scripts

	# Sums scripts
	find ../base/sums/scripts -mindepth 1 \( -path \*CVS -prune -o -exec ln -s {} . \; -a -exec echo "  " {} " OK" \; \) -o -exec echo "  " {} " ERROR - couldn't create link" \;

	# Util scripts
	find ../base/util/scripts -mindepth 1 \( -path \*CVS -prune -o -exec ln -s {} . \; -a -exec echo "  " {} " OK" \; \) -o -exec echo "  " {} " ERROR - couldn't create link" \;

        # Export scripts
	find ../base/export/scripts -mindepth 1 \( -path \*CVS -prune -o -exec ln -s {} . \; -a -exec echo "  " {} " OK" \; \) -o -exec echo "  " {} " ERROR - couldn't create link" \;

	# DRMS / IDL-interface scripts
	find ../base/drms/scripts -mindepth 1 \( -path \*CVS -prune -o -exec ln -s {} . \; -a -exec echo "  " {} " OK" \; \) -o -exec echo "  " {} " ERROR - couldn't create link" \;

	# Project-specific scripts - these won't show up if you don't have the project source.
	if (-x ${LOCALIZATIONDIR}/configure) then
	    ${LOCALIZATIONDIR}/configure
	endif

	cd ..
	echo "done"
	echo

        echo "Setting links to headers..."

        cd base/include
        find .. -path '../include' -prune -o -name \*.h  \( \( -exec ln -s {} . \; -a -exec echo "  " {} " OK" \; \) -o -exec echo "  " {} " ERROR - couldn't create link" \; \)
        cd ../..

        cd include
        if (-d ../base/include) then
            find ../base/include -name \*.h -exec ln -s {} . \;
        endif
        cd ..
        echo "done"
        echo

        # Custom make variable values - these will override the values set in moreconfigure.pl
        # echo "JSOC_COMPILER = gcc" >> custom.mk
        # echo "JSOC_FCOMPILER = gfortran" >> custom.mk
        # echo "DRMS_DEFAULT_RETENTION = -10" >> custom.mk

    endif


